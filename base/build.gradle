apply plugin: 'com.android.library'

apply plugin: 'kotlin-kapt'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply from: 'dependencies.groovy'

android {
    compileSdkVersion rootProject.androidcfg.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.androidcfg.minSdkVersion
        targetSdkVersion rootProject.androidcfg.targetSdkVersion
        versionCode rootProject.androidcfg.versionCode
        versionName rootProject.androidcfg.versionName

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main.java.srcDirs = ['src/main/kotlin', 'src/main/java']
    }
}

kotlin {
    experimental {
        coroutines "enable"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    implementation_list.forEach {
        switch (it) {
            case dependencies_map['lifecycle-extensions']:
                implementation it, {
                    exclude module: 'support-fragment'
                }
                break
            default:
                implementation it
        }
    }
}
////////////// maven仓库推送 //////////////

apply from: "$configPath/upload.gradle"

group = "com.wittyneko"
archivesBaseName = "ktx-base"
version = "1.0.0-SNAPSHOT"
